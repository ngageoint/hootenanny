// Load shared libraries
@Library('radiant-pipeline-library')_

pipeline {
    agent { label 'master' }

    environment {
        SYS_URL = 'UNKOWN'
        SYS_VER = 'UNKOWN'
    }

    triggers {
        // Set nightly trigger
        cron((BRANCH_NAME == "master" || BRANCH_NAME == "develop") ? "H H(20-21) * * 1-5" : "")
    }

    // Configurable parameters for users to skip steps and control pipeline behavior
    parameters {
        booleanParam(name: 'Destroy_VM', defaultValue: true)
        booleanParam(name: 'Hoot_provision', defaultValue: true)
        booleanParam(name: 'Configure_Tests', defaultValue: false)
        booleanParam(name: 'Build', defaultValue: true)
        booleanParam(name: 'Core_tests', defaultValue: true)
        booleanParam(name: 'Services_tests', defaultValue: true)
        booleanParam(name: 'UI_tests', defaultValue: true)
        booleanParam(name: 'Sonar', defaultValue: false)
        string(name: 'Box', defaultValue: 'default', description: 'Vagrant Box')
    }

    stages {
        stage("Destroy VM") {
            when { expression { return params.Destroy_VM } }
            steps {
                // Existing VM may or may not exist depending on previous runs
                sh "vagrant destroy -f ${params.Box} || true"
            }
        }
        stage("Setup") {
            steps {
                
                // Make sure we have the tags info because figuring out the version is required in the build process
                // Remove any screenshots from previous runs
                sh '''
                    git fetch --tags
                    git submodule update --init
                    cp -R ../software.ubuntu1404 software
                    rm -rf ./test-files/ui/screenshot_*.png
                '''
            }
        }
        stage("Hoot Provision") {
            when { expression { return params.Hoot_provision } }
            steps {
                // NOTE: Only installs hoot build dependencies
                sh "vagrant up ${params.Box} --provision-with software,hoot --provider aws"
                script {
                    SYS_VER = sh(returnStdout: true, script: "vagrant ssh ${params.Box} -c 'cd hoot; ./scripts/git/GitVersion.sh'")
                    SYS_URL = sh(returnStdout: true, script: "vagrant ssh-config ${params.Box} | grep HostName | awk '{print \$2}'")
                    SYS_URL = SYS_URL.trim()
                    echo "System under test version ${SYS_VER}  http://${SYS_URL}:8080/hootenanny-id/"
                }
            }       
        }
        stage("Test Configure") {
            when { 
                anyOf {
                    expression { return params.Configure_Tests }
                    // Work around to run certain tests during nightly cron triggers but not on commits to develop.  Logic can be udpated
                    // once this https://issues.jenkins-ci.org/browse/JENKINS-41272 is implemented
                    expression { return Calendar.instance.get(Calendar.HOUR_OF_DAY) in 20..23 } 
                }
            }
            steps {
                sh "vagrant ssh ${params.Box} -c 'cd hoot; source ./SetupEnv.sh; ./scripts/TestConfigure.sh &> Hoot_Config_Test || { cat Hoot_Config_Test; false; }'"
            }
        }
        stage("Sonar Config") {
            when {
                anyOf {
                    // Configure for sonar if user selected it, pull-request, or nightly run of develop
                    expression { return params.Sonar }
                    changeRequest()
                    allOf {
                        expression { return Calendar.instance.get(Calendar.HOUR_OF_DAY) in 20..22 }
                        branch 'develop'
                    }
                }
            }
            steps{
                sh "vagrant ssh ${params.Box} -c 'cd hoot; ./scripts/sonar/sonar-install.sh'"
                // Compile with build watcher to allow for sonar scanning in subsequent steps
                sh "sed -i '/make -s clean && make -sj\$(nproc)/c\\make -s clean && build-wrapper-linux-x86-64 --out-dir bw-output make -sj\$(nproc)' VagrantBuild.sh"
                script {
                    if (env.BRANCH_NAME == "develop") {
                        // Compile instrumented code for develop branch which allows for calculating coverage
                        sh "sed -i 's/--with-uitests/--with-uitests --with-coverage/g' VagrantBuild.sh"
                    }
                }
            }
        }
        stage("Build") {
            when { expression { return params.Build } }
            steps {
                // Perform remainder of provisioning
                sh "vagrant provision ${params.Box} --provision-with build,EGD,tomcat"
            }       
        }
        stage("Core Tests") {
            when { expression { return params.Core_tests } }
            steps {
                sh "vagrant ssh ${params.Box} -c 'cd hoot; source ./SetupEnv.sh; hoot version --debug; bin/HootTest --diff --glacial --parallel \$(nproc)'"
            }
        }
        stage("Services Tests") {
            when { expression { return params.Services_tests } }
            steps {
                sh "vagrant ssh ${params.Box} -c 'cd hoot; make -sj`nproc` plugins-test'"
                sh "vagrant ssh ${params.Box} -c 'cd hoot; make -sj`nproc` services-test'"
            }
        }
        stage("UI Tests") {
            when { expression { return params.UI_tests } }
            steps {
                sh "vagrant ssh ${params.Box} -c 'cd hoot; make -s ui-test'"
            }
        }
        stage("Sonar") {
            when {
                anyOf {
                    expression { return params.Sonar }
                    changeRequest()
                    allOf {
                        expression { return Calendar.instance.get(Calendar.HOUR_OF_DAY) in 20..23 }
                        branch 'develop'
                    }
                }
            }
            steps{
                    sh "vagrant ssh ${params.Box} -c 'cd hoot; ./SetupEnv.sh; cd hoot-services; mvn sonar:sonar \
                        -Dsonar.host.url=https://sonarcloud.io \
                        -Dsonar.organization=hootenanny \
                        -Dsonar.login=${env.SONAR_CLOUD_KEY} \
                        -Dsonar.projectKey=hoot-services \
                        -Dsonar.branch.name=${env.BRANCH_NAME}'"

                script {
                    if (env.BRANCH_NAME.startsWith("PR-")) {
                        // PR scan will use github plugin
                        sh "vagrant ssh ${params.Box} -c 'cd hoot; ./SetupEnv.sh; ./scripts/sonar/sonar-scan.sh -l ${env.SONAR_CLOUD_KEY} -b ${env.BRANCH_NAME} -p ${env.CHANGE_ID} -a ${env.DGIS_BOT_TOKEN}'"
                    }
                    else {
                        sh "vagrant ssh ${params.Box} -c 'cd hoot; ./SetupEnv.sh; ./scripts/cover/coverGcov.sh'"
                        sh "vagrant ssh ${params.Box} -c 'cd hoot; ./SetupEnv.sh; ./scripts/sonar/sonar-scan.sh -l ${env.SONAR_CLOUD_KEY} -b ${env.BRANCH_NAME}'"
                    }

                }
            }
        }
    }
    post {
        always {
            script {
                if (!env.BRANCH_NAME.startsWith("PR-")) {
                    // Send build notification
                    notifySlack(currentBuild.result, "#builds_hoot")
                }
            }
        }
        success {
            script {
                if (env.BRANCH_NAME.startsWith("release")) {
                    // Don't clean up this workspace.  We want to leave it up for acceptance testing
                    slackSend channel: "#builds_hoot", message: "Release candidate version ${SYS_VER} ready for testing http://${SYS_URL}:8080/hootenanny-id/"
                } else {
                    // If all tests passed, clean everything up
                    sh "vagrant destroy -f ${params.Box}"
                    cleanWs()
                }
            }
        }
        failure {
            script {
                if (!env.BRANCH_NAME.startsWith("PR-")) {
                    // Copy over any UI failure screenshots and send to slack
                    sh "vagrant scp ${params.Box}:~/hoot/test-files/ui/screenshot_*.png ./test-files/ui/"
                    postSlack("${env.WORKSPACE}/test-files/ui/", "screenshot_*.png", "${env.JENKINS_BOT_TOKEN}", "#builds_hoot")
                }
            }
        }
    }
}
